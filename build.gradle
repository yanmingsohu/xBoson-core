apply plugin: 'war'

defaultTasks 'war'


buildscript {
    repositories {
        flatDir dirs: 'C:\\Programs64\\Java\\proguard6.0.1\\lib'
    }
    dependencies {
        classpath ':proguard'
    }
}


sourceSets {
    main {
        java {
            srcDirs = ['src']
        }
        compileClasspath = fileTree('WebRoot/WEB-INF/lib/') + fileTree('libs/')
    }
}


tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}


task copyResource(type: Copy) {
    from 'src'
    into 'build/guard-out/classes/classes'

    include '**/*.js'
    include '**/*.sql'
    include '**/*.lua'
    include '**/*.properties'
    include '**/*.json'
    includeEmptyDirs = false
}


war {
    webInf {
        from('WebRoot/WEB-INF')
        exclude 'classes'
    }
    archiveName = 'xboson.war'
    classpath = 'build/guard-out/classes/classes/'
}


task proguard(type: proguard.gradle.ProGuardTask) {
    injars  'build/classes/java/main'
    outjars 'build/guard-out/'

    libraryjars "${System.getProperty('java.home')}/lib/"
    libraryjars "${System.getProperty('java.home')}/jre/lib/"
    libraryjars './WebRoot/WEB-INF/lib/'
    libraryjars './libs/'

    dontusemixedcaseclassnames
    printmapping 'build/obfuscate-mapping.txt'
    printseeds 'build/seed.txt'
    obfuscationdictionary 'obfuscate-keywords.txt'
    classobfuscationdictionary 'class-keywords.txt'

    // 优化次数: 5:12s | 10:18s | 20:26s | 99:93s
    optimizationpasses 99

    // Database
    keep 'public class com.xboson.db.driver.* { public *; }'
    keep 'public class com.xboson.db.ConnectConfig { public *; }'
    keeppackagenames 'com.xboson.db.sql'

    // Script
    keep 'public class com.xboson.script.JSObject { public *; }'
    keepclassmembers 'public class com.xboson.script.lib.* { public *; }'
    keepclassmembers 'public class com.xboson.app.lib.* { public *; }'
    keep 'interface com.xboson.script.IVisitByScript'
    keepclassmembers 'class * implements com.xboson.script.IVisitByScript {*;}'
    keeppackagenames 'com.xboson.app.lib'
    keeppackagenames 'com.xboson.script.lib'

    // File system
    keep 'public class com.xboson.fs.* { public *; }'
    keep 'public class com.xboson.fs.redis.FindContentInRedisWithLua'
    keep 'public class com.xboson.fs.ui.UIFileFactory'
    keep 'public class com.xboson.j2ee.ui.MimeTypeFactory'
    keep 'interface com.xboson.fs.basic.* { public *; }'

    // init
    keep 'public class com.xboson.init.install.step.* { public *; }'
    keep 'public class com.xboson.init.Startup'

    // Test / Service / log / Sleep
    keep 'public class com.xboson.test.* { public *; }'
    keepclassmembers 'public class com.xboson.service.* { public *; }'
    keep 'class * implements com.xboson.sleep.ISleepwalker { public *; }'
    keep 'class com.xboson.log.writer.* { public *; }'
    keeppackagenames 'com.xboson.test.js'

    // RPC
    keep 'class com.xboson.rpc.* { public *; *; }'
    keep 'interface com.xboson.rpc.* { public *; *; }'
    keep 'class * implements java.rmi.Remote { *; public *; }'

    // Been
    keep 'public class com.xboson.been.XBosonException { public *; }'
    keep 'public class com.xboson.been.Config { public *; }'
    keep 'public class com.xboson.been.LoginUser { public *; }'
    keep 'public class com.xboson.been.MongoConfig { public *; }'
    keep 'public class com.xboson.been.SessionData { public *; }'
    keep 'public class com.xboson.been.AppToken { public *; }'
    keepclassmembers 'public class com.xboson.been.License { public *; }'

    keepattributes '*Annotation*'
    renamesourcefileattribute 'SourceFile'
    keepattributes 'SourceFile, LineNumberTable, Exceptions, Signature'
    keep 'public class * implements java.io.Serializable { public *; *; }'
    keepclassmembers 'class * implements java.io.Serializable'
    keepclassmembers 'enum * { public *; }'

}


task remove_proguard_out(type: Delete) {
    delete 'build/guard-out'
}


war.dependsOn proguard
war.dependsOn copyResource
copyResource.dependsOn proguard
proguard.dependsOn remove_proguard_out
proguard.dependsOn compileJava